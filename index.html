<!doctype html>
<meta charset="utf-8">
<title>Countdown</title>
<meta name="viewport" content="width=device-width">
<style>
	* { padding: 0; margin: 0; border: 0; width: 100%; height: 100%; vertical-align: bottom; }
	.timer { text-align: center; font-size: 5em; }
	.error { color: red; }
	.alarm { color: yellow; background: black; }
	/*.running { display: block; position: absolute; bottom: 0; height: 1em; background: lime; }*/
</style>

<script src="lib/jquery-2.1.3.min.js"></script>
<script src="lib/juration.js"></script>
<script src="lib/coffee-script.js"></script>

<script type="text/coffeescript">
	
	class Timer
		constructor: ->
			@total_seconds = 0
			@elapsed_seconds = 0
			@started_seconds = 0
			@started_time = 0
			@el = ($ "<input class=timer type=text>").appendTo "body"
			@el.on "keydown", (e)=>
				start = not @iid?
				@stop()
				@el.removeClass "error alarm"
				if e.keyCode is 13
					e.preventDefault()
					if start
						try
							total_time = @el.val() ? location.hash
							if total_time in ["0", ""]
								@set location.hash
							else
								@set total_time
								@start()
								location.hash = total_time
						catch e
							@el.addClass "error"
							console?.error? e

			
		update: ->
			text = juration.stringify @total_seconds - @elapsed_seconds
			@el.val text
			@el.css fontSize: @el.width() / 10
		
		set: (total_time)->
			@stop()
			if typeof total_time is "number"
				@total_seconds = total_time
			else
				@total_seconds = juration.parse total_time
			@update()
		
		start: ->
			@stop()
			@started_seconds = @elapsed_seconds
			@started_time = Date.now()
			@iid = setInterval =>
				@elapsed_seconds = (Date.now() - @started_time) / 1000 + @started_seconds
				if @elapsed_seconds >= @total_seconds
					@stop()
					@el.val 0
					@el.addClass "alarm"
				else
					@update()
			, 150
			@el.addClass "running"
		
		stop: ->
			@elapsed_seconds = 0
			@started_seconds = 0
			@started_time = 0
			clearInterval @iid
			@iid = null
			@el.removeClass "running"
	
	timer = new Timer
	if location.hash
		timer.set location.hash
		timer.start()
	
	$(window).on "hashchange", ->
		timer.set location.hash
		timer.start()
		
	
</script>
