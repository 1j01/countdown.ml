<!doctype html>
<meta charset="utf-8">
<title>Countdown</title>
<meta name="viewport" content="width=device-width">
<style>
	* {
		padding: 0;
		margin: 0;
		border: 0;
		width: 100%;
		height: 100%;
		vertical-align: bottom;
	}
	.timer input {
		text-align: center;
		font-size: 5em;
	}
	.error input {
		color: red;
	}
	.alarm input {
		color: yellow;
		background: rgb(215, 57, 102);
		animation: alarm 1s ease-out infinite;
		-webkit-animation: alarm 1s ease-out infinite;
	}
	.timer::after {
		content: "";
		display: block;
		position: absolute;
		bottom: 0;
		width: 100%;
		height: 1in;
		pointer-events: none;
		background: rgb(255, 243, 188);
	}
	.timer.running::after {
		background: rgb(0, 243, 188);
	}
	.timer.alarm.alarm::after {
		background: yellow;
	}
	* {
		transition: all .2s ease-in-out;
	}
	*::after {
		transition: all .1s ease-in-out;
	}
	@-webkit-keyframes alarm {
		0% { text-shadow: 0 0 0px yellow; }
		100% { text-shadow: 0 0 300px rgba(255, 255, 0, 0); }
	}
	@keyframes alarm {
		0% { text-shadow: 0 0 0px yellow; }
		100% { text-shadow: 0 0 300px rgba(255, 255, 0, 0); }
	}
</style>

<script src="lib/jquery-2.1.3.min.js"></script>
<script src="lib/juration.js"></script>
<script src="lib/coffee-script.js"></script>

<script type="text/coffeescript">
	
	after = (ms, fn)-> tid = setTimeout fn, ms; stop: -> clearTimeout tid
	every = (ms, fn)-> iid = setInterval fn, ms; stop: -> clearInterval iid
	
	debug = (args...)-> console?.debug? args...
	
	Notification = window.Notification ? window.mozNotification ? window.webkitNotification
	
	class Countdown
		constructor: ->
			every 150, =>
				status = if @interval then "running" else if @paused then "paused" else "stopped"
				console.info "Status: #{status}, elapsed: #{@elapsed_seconds}s / #{@total_seconds}s"
				if @paused and @interval
					console.warn "Paused while running!"
			
			@total_seconds = 0
			@elapsed_seconds = 0
			@paused = no
			
			@audio = new Audio
			@audio.src = "audio/church-bells-and-birds.mp3"
			
			@el = ($ "<div class=timer>").appendTo "body"
			@input = ($ "<input class=time type=text>").appendTo @el
			@input.focus()
			
			($ window).on "keydown", (e)=>
				debug e.keyCode
				unless document.activeElement.tagName.match /input|textarea/i
					debug "(no textarea active)"
					if e.keyCode in [13, 32] # enter, space
						if @paused or @elapsed_seconds is 0
							if @isAt0()
								@reset()
							else
								@start()
						else
							@pause()
					if e.keyCode in [8] # backspace
						@input.focus()
			
			@input.on "keydown", (e)=>
				start = not @interval?
				return if e.ctrlKey or e.altKey or e.shiftKey
				@pause()
				@el.removeClass "error alarm"
				if e.keyCode is 13
					e.preventDefault()
					
					# if @isAt0()
					# 	@set location.hash
					# else
					if start
						total_time = @input.val() ? location.hash
						try
							@set total_time
							@start()
							location.hash = total_time
						catch e
							@el.addClass "error"
							console?.error? e
		
		update: ->
			text = juration.stringify @total_seconds - @elapsed_seconds
			@input.val text
			@input.css fontSize: @el.width() / 10
		
		set: (total_time)->
			@stop()
			debug "SET!", total_time
			@el.removeClass "error alarm"
			@elapsed_seconds = 0
			
			if typeof total_time is "number"
				@total_seconds = total_time
			else
				@total_seconds = juration.parse total_time
			
			@update()
			
			@input.blur()
			@input.on "focus", (e)=>
				if @isAt0()
					@reset()
				else
					@pause()
			
			Notification.requestPermission()
			@notification?.close()
			@audio.pause()
			@audio.currentTime = 0
		
		start: ->
			@stop()
			debug "START!"
			started_seconds = @elapsed_seconds
			started_time = Date.now()
			@interval = every 30, =>
				@elapsed_seconds = (Date.now() - started_time) / 1000 + started_seconds
				if @elapsed_seconds >= @total_seconds
					@alarm()
				else
					@update()
			@el.addClass "running"
		
		pause: ->
			debug "PAUSE!"
			@paused = yes
			@interval?.stop()
			@interval = null
			@el.removeClass "running alarm"
		
		stop: ->
			@pause()
			debug "STOP!"
			@paused = no
		
		reset: ->
			@set location.hash
		
		alarm: ->
			@stop()
			@elapsed_seconds = 0
			@input.val "0"
			
			@el.addClass "alarm"
			
			if Notification.permission is "granted"
				@notification = new Notification "Hey!", body: "The timer's up!"
				@notification.onclick = @notification.onclose = => @reset()
			else
				alert "Hey! The timer's up!"
			
			@audio.play()
		
		isAt0: ->
			@input.val() in ["0", ""]
	
	c = new Countdown
	
	set_from_hash = ->
		c.set location.hash
		c.start()
	
	set_from_hash() if location.hash
	($ window).on "hashchange", set_from_hash
	
</script>
