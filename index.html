<!doctype html>
<meta charset="utf-8">
<title>Countdown</title>
<meta name="viewport" content="width=device-width">
<style>
	* {
		padding: 0;
		margin: 0;
		border: 0;
		width: 100%;
		height: 100%;
		vertical-align: bottom;
	}
	.timer input { text-align: center; font-size: 5em; }
	.error input { color: red; }
	.alarm input { color: yellow; background: black; }
	.timer::after {
		content: "";
		display: block;
		position: absolute;
		bottom: 0;
		width: 100%;
		height: 1in;
		pointer-events: none;
		background: rgb(255, 243, 188);
	}
	.timer.running::after {
		background: rgb(0, 243, 188);
	}
	.timer.alarm.alarm::after {
		background: yellow;
	}
	* {
		transition: all .2s ease-in-out;
	}
	*::after {
		transition: all .1s ease-in-out;
	}
</style>

<script src="lib/jquery-2.1.3.min.js"></script>
<script src="lib/juration.js"></script>
<script src="lib/coffee-script.js"></script>

<script type="text/coffeescript">
	
	after = (ms, fn)-> tid = setTimeout fn, ms; stop: -> clearTimeout tid
	every = (ms, fn)-> iid = setInterval fn, ms; stop: -> clearInterval iid
	
	class Countdown
		constructor: ->
			@total_seconds = 0
			@elapsed_seconds = 0
			@paused = no
			
			@el = ($ "<div class=timer>").appendTo "body"
			@input = ($ "<input class=time type=text autofocus>").appendTo @el
			
			($ window).on "keydown", (e)=>
				console.log e.keyCode
				unless document.activeElement.tagName.match /input|textarea/i
					if e.keyCode in [13, 32] # enter, space
						if @paused or @elapsed_seconds is 0
							@start()
						else
							@stop()
			
			@input.on "keydown", (e)=>
				start = not @iid?
				return if e.ctrlKey or e.altKey or e.shiftKey
				@pause()
				@el.removeClass "error alarm"
				if e.keyCode is 13
					e.preventDefault()
					total_time = @input.val() ? location.hash
					if total_time in ["0", ""]
						@set location.hash
					else if start
						try
							@set total_time
							@start()
							location.hash = total_time
						catch e
							@el.addClass "error"
							console?.error? e
		
		update: ->
			text = juration.stringify @total_seconds - @elapsed_seconds
			@input.val text
			@input.css fontSize: @el.width() / 10
		
		set: (total_time)->
			@stop()
			if typeof total_time is "number"
				@total_seconds = total_time
			else
				@total_seconds = juration.parse total_time
			@update()
		
		start: ->
			@stop()
			started_seconds = @elapsed_seconds
			started_time = Date.now()
			@interval = every 30, =>
				@elapsed_seconds = (Date.now() - started_time) / 1000 + started_seconds
				if @elapsed_seconds >= @total_seconds
					@alarm()
				else
					@update()
			@el.addClass "running"
		
		pause: ->
			@paused = yes
			@interval?.stop()
			@interval = null
			@el.removeClass "running"
		
		stop: ->
			@pause()
			@elapsed_seconds = 0
			@paused = no
		
		alarm: ->
			@stop()
			@input.val "0"
			@el.addClass "alarm"
			# TODO: sound
			# TODO: desktop notification
	
	c = new Countdown
	
	set_from_hash = ->
		c.set location.hash
		c.start()
	
	set_from_hash() if location.hash
	($ window).on "hashchange", set_from_hash
	
</script>
